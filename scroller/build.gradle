apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'eclipse'

mainClassName = 'rob.scroller.ScrollerGame'
sourceCompatibility = 1.6

repositories {   mavenCentral()   }

dependencies {
	compile group: 'org.lwjgl.lwjgl', name: 'lwjgl', version: '2.8.3'
	compile group: 'org.lwjgl.lwjgl', name: 'lwjgl_util', version: '2.8.3'
	compile group: 'com.google.guava', name: 'guava', version: '13.0.1'
	compile group: 'com.google.inject', name: 'guice', version: '3.0'
	compile group: 'org.jbox2d', name: 'jbox2d-library', version: '2.1.2.2'
	compile fileTree(dir: 'lib', include: '*.jar')
}

def getPlatformNativeDir(def platform)
{
	"$buildDir/natives/$platform"
}

def platforms = ['windows', 'linux', 'osx']

platforms.each { platform ->
	
	task "${platform}Natives" {
		
		outputs.dir getPlatformNativeDir(platform) 
		
		doLast {	
			copy {
				def artifacts = configurations.compile.resolvedConfiguration.resolvedArtifacts
					.findAll { it.classifier == "natives-$platform" }
				
				artifacts.each { from zipTree(it.file) }
				into getPlatformNativeDir(platform)
			}
		}
	}
}

task natives {
	description "Copies native libraries to an appropriate directory."
	
	def nativeTasks = platforms.collect { "${it}Natives" }.findAll { tasks[it] }.collect{ tasks[it] }
	
	dependsOn nativeTasks

	nativeTasks.collect { it.outputs.files }.findAll { it.each { outputs.file it } }
}

processResources.dependsOn natives

// make the task "installApp" create a working application and put the natives in the executable directory
// yes, this is not really nice but works for now
applicationDistribution.from(natives) { into "bin" }

// make the task "run" work
run.jvmArgs("-Djava.library.path=" + platforms.collect { platform -> getPlatformNativeDir(platform) }.join(File.pathSeparator))

eclipse {
	classpath {
		downloadSources = true
		downloadJavadoc = true
	}
}

// handle eclipse configuration with native libs
// you have to add these natives to your launch configuration
tasks['eclipse'].dependsOn natives
tasks['eclipse'].doLast {
	
	platforms.each { platform ->
		copy {
			from getPlatformNativeDir(platform)
			into "lib/natives/$platform"
		}
	}
}


